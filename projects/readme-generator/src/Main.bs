
outputDir = "./output"
if(args.Size() == 1)
{
	outputDir = args[0]
}
else
{
	Console.WriteLine("No Output Directory Specified")
	return -1
}

knownItems = []
FileSystem = Environment.LoadInterface("FileSystem", {})
Path = Environment.LoadInterface("Path", {})
function knowsItem(item)
{
	foreach	v in knownItems
	{
		if(item == v)
		{
			return true
		}
	}
	return false
}

function repeatChar(char, count)
{
	ret = ""
	for i = 0 while< count
	{
		ret += char
	}
	return ret
}

function writeArrayContent(arr)
{
	return Environment.Debug(arr)
}

function writeArray(key, arr)
{
	return "Property '" + key + "' with default value \n```js\n" + writeArrayContent(arr) + "\n```\n"
}

function writeTableContent(table)
{
	return Environment.Debug(table)
}

function writeTable(key, table)
{
	return "Property '" + key + "' with default value \n```js\n" + writeTableContent(table) + "\n```\n"
}

function writeTypeContent(type)
{
	return Environment.Debug(type)
}

function writeType(key, type)
{
	return "Property '" + key + "' with default value \n```js\n" + writeTypeContent(type) + "\n```\n"
}


function writeFunction(fnc)
{
	return "Function Signature: '`" + Environment.Debug(fnc) + "`'"
}


function writeLiteralContent(value)
{
	return value
}
function writeLiteral(key, value)
{
	return "Property '`" + key + "`' with default value '`" + value + "`'\n"
}


function makeSection(key, value, depth)
{
	header = repeatChar("#", depth) + " " + key + "\n"
	body = ""

	if(Convert.IsArray(value))
	{
		body = writeArray(key, value)
	}
	else if(Convert.IsTable(value))
	{
		body = makeReadme(key, value, depth + 1)
	}
	else if(Convert.IsType(value))
	{
		body = writeType(key, value)
	}
	else
	{
		Console.WriteLine("Can not Generate Readme for '" + key + "' = '" + Environment.Debug(value) + "'")
	}

	footer = "\n______\n"

	return "\n" + header + body + footer
}

function makeReadme(name, table, depth)
{
	if(knowsItem(table))
	{
		return "\n<RECURSION: " + name + ">\n"
	}
	knownItems.Add(table)
	header = repeatChar("#", depth) + " `" + name + "` Documentation"
	rm = "\n"
	literalData = {}
	funcData = {}
	foreach k in table
	{
		try
		{
			v = table[k]
			if(Convert.IsTable(v))
			{
				rm += makeReadme(k, v, depth + 1)
			}
			else if(Convert.IsLiteral(v))
			{
				literalData[k] = v
			}
			else if(Convert.IsFunction(v))
			{
				funcData[k] = v
			}
			else
			{
				rm += makeSection(k, v, depth + 1)
			}
		}
		catch(e)
		{
			Console.WriteLine("Error: " + Environment.Debug(e))
		}
	}

	props = repeatChar("#", depth + 1) + " Properties:"
	hasProps = false
	foreach (k, v) in literalData
	{
		hasProps = true
		line = "\n- `" + k + " = " + v + "`"
		props += line
	}
	props += "\n"

	funcs = repeatChar("#", depth + 1) + " Functions:"
	hasFuncs = false
	foreach (k, v) in funcData
	{
		hasFuncs = true
		line = "\n- " + writeFunction(v)
		funcs += line
	}
	funcs += "\n"

	rs = ""

	if(hasFuncs)
	{
		rs += funcs
	}

	if(hasProps)
	{
		rs += props
	}
	return header + "\n" + rs + "\n" + rm
}

function makeInterfaceReadme(iface)
{
	Console.WriteLine("Generating Docs for " + iface)
	interface = Environment.LoadInterface(iface, {})
	str = makeReadme(iface, interface, 1)
	FileSystem.WriteAll(Path.Combine(outputDir, iface + ".md"), str)
}

if(!FileSystem.Exists(outputDir))
{
	FileSystem.CreateDir(outputDir)
}

interfaces = Environment.GetInterfaceNames()
foreach	iface in interfaces
{
	makeInterfaceReadme(iface)
}

return 1