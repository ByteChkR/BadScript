log = modules.require("logging").create("INTERACTIVE")
path = environment.loadInterface("path", {})

dataPath = path.combine(path.getAppPath(), "bs-data", "app-data", "interactive")
pluginPath = path.combine(dataPath, "plugins")

interactiveScope = null
interactiveScript = null
function readExpression()
{
	while(1)
	{
		write("BS> ")
		line = read()
		if(	string.toUpper(line) == "\\EXEC" ||
			string.toUpper(line) == "\\RUN")
		{
			try
			{
				environment.loadScopedString(interactiveScope, interactiveScript)
			}
			catch (e)
			{
				log(string.format("ERROR: '{0}' with message: '{1}'", e.type, e.message))
			}
			interactiveScript = ""
		}
		else if(string.toUpper(line) == "\\QUIT" ||
				string.toUpper(line) == "\\EXIT")
		{
			log("Exit..")
			break
		}
		else if(string.toUpper(line) == "\\RESET" ||
				string.toUpper(line) == "\\CLEAR")
		{
			log("Clearing Script Content..")
			interactiveScript = ""
		}
		else if(string.toUpper(line) == "\\RESTART" ||
				string.toUpper(line) == "\\RELOAD")
		{
			return 1
		}
		else if(string.toUpper(line) == "\\PLUGIN_RELOAD" ||
				string.toUpper(line) == "\\PL_RELOAD")
		{
			loadPlugins()
		}
		else
		{
			interactiveScript = interactiveScript + "\n" + line
		}
	}
}

function loadPlugins()
{
	fs = environment.loadInterface("fs", {})
	fs.createDir(pluginPath)
	files = fs.getFiles(pluginPath, "*.bs", 1)
	foreach file in files
	{
		log("Loading Plugin: " + path.getFileName(file))
		environment.loadScopedString(interactiveScope, fs.readAll(file))
	}
}

function loadInteractiveSession()
{
	log("Loading Interfaces.. ")
	environment.loadInterface("string")

	log("Creating Sandbox Scope.. ")
	interactiveScope = environment.createScope()
	interactiveScript = ""
	
	loadPlugins()


	log("Starting Session..")
	log("\tQuit with \\QUIT or \\EXIT")
	log("\tExecute Script with \\EXEC or \\RUN")
	log("\tDelete Written Script with \\RESET or \\CLEAR")
	log("\tReload Shell Session with \\RELOAD or \\RESTART")
	log("\tReload Shell Session with \\PLUGIN_RELOAD or \\PL_RELOAD")
	return readExpression()
}

function runShell()
{
	log("Initializing BS Interactive Shell..")

	while(loadInteractiveSession())
	{
		log("Restarting Session..")
	}
}