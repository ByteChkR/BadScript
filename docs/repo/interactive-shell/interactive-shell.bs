log = modules.require("logging").create("INTERACTIVE")


interactiveScope = null
interactiveScript = null
function readExpression()
{
	while(1)
	{
		write("BS> ")
		line = read()
		if(	string.toUpper(line) == "\\EXEC" ||
			string.toUpper(line) == "\\RUN")
		{
			try
			{
				environment.loadScopedString(interactiveScope, interactiveScript)
			}
			catch (e)
			{
				log(string.format("ERROR: '{0}' with message: '{1}'", e.type, e.message))
			}
			interactiveScript = ""
		}
		else if(string.toUpper(line) == "\\QUIT" ||
				string.toUpper(line) == "\\EXIT")
		{
			log("Exit..")
			break
		}
		else if(string.toUpper(line) == "\\RESET" ||
				string.toUpper(line) == "\\CLEAR")
		{
			log("Clearing Script Content..")
			interactiveScript = ""
		}
		else if(string.toUpper(line) == "\\RESTART" ||
				string.toUpper(line) == "\\RELOAD")
		{
			return 1
		}
		else
		{
			interactiveScript = interactiveScript + "\n" + line
		}
	}
}

function loadInteractiveSession()
{
	log("Loading Interfaces.. ")
	environment.loadInterface("string")

	log("Creating Sandbox Scope.. ")
	interactiveScope = environment.createScope()
	interactiveScript = ""
	
	log("Starting Session..")
	log("\tQuit with \\QUIT or \\EXIT")
	log("\tExecute Script with \\EXEC or \\RUN")
	log("\tDelete Written Script with \\RESET or \\CLEAR")
	log("\tReload Shell Session with \\RELOAD or \\RESTART")
	return readExpression()
}

function runShell()
{
	log("Initializing BS Interactive Shell..")

	while(loadInteractiveSession())
	{
		log("Restarting Session..")
	}
}